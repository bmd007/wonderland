plugins {
	id "java"
	id "idea"
	id "org.springframework.boot" version "2.6.3"
	id "io.spring.dependency-management" version "1.0.11.RELEASE"
	id "com.gorylenko.gradle-git-properties" version "2.4.0"
	id "jacoco" //code coverage
}

jacoco {
	toolVersion = "0.8.7"
}

group = 'wonderland'

ext.buildNumber = {
	def buildNumber = System.getenv('VERSION')
	if (buildNumber == null || buildNumber.allWhitespace) {
		buildNumber = 'beta'
	}
	return buildNumber
}

project.version = buildNumber()

sourceCompatibility = 17

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2021.0.0")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation "org.springframework.cloud:spring-cloud-starter-consul-config"
	implementation "org.springframework.cloud:spring-cloud-starter-consul-discovery"
	implementation 'org.springframework.cloud:spring-cloud-starter'
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation "org.springframework.cloud:spring-cloud-loadbalancer"

	implementation 'org.apache.kafka:kafka-streams'
	implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.webjars.bower:github-com-happyworm-jPlayer:2.9.2'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	implementation 'com.google.guava:guava:31.0.1-jre'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'com.xebialabs.restito:restito:0.9.4'
	testImplementation('org.mockito:mockito-junit-jupiter')
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
}

wrapper {
	distributionType = Wrapper.DistributionType.ALL
}

bootBuildImage {
//	imageName = "bmd007/${project.name}:${project.version}"
	imageName = "bmd007/${project.name}"
	environment = ["BP_JVM_VERSION" : "17"]
}
